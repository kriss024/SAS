/* 
INPUT INFORMAT
01/23/1963  mmddyy10.
1/23/1963  mmddyy10.
01/23/63  mmddyy8.
1/23/63  mmddyy8.
January 23,1963  worddate20.
jan 23, 1963  worddate12.
23jan1963  date9.
23jan63  date7.
23-jan-1963  date11.
01-23-63  mmddyy8.
19630123  yymmdd8.
*/


/* U¯YWANIE DATY JAKO PARAMETR W KODZIE SAS */

DATA DATES1A;
A='12MAR1981'd;
B=A;
C=' 12MAR1981  ';
D=STRIP(C);
E=INPUT(D, DATE9.);
F=E+5;
F_SYS = "&sysdate"d;
G_SYS9 = "&sysdate9"d; 
FORMAT B DATE9.; /*NADAWANIE FORMATU DATY, FIZYCZNIE KOLUMNA NADAL JEST LICZB¥ */
FORMAT E DATE9.;
FORMAT F DATE9.;
T=DATE();
FORMAT T DATE9.;
FORMAT F_SYS DATE9.;
FORMAT G_SYS9 DATE9.;
DIFF=F-E;
RUN;

/*£¥CZNIE DATY I GODZINY W DATETIME */

DATA DATES1B;
DT=DATETIME();
DT_FORMATED=DT;
DATE=DATEPART(DT);
TIME=TIMEPART(DT);
SAS_DT=DHMS(DATE,0,0,TIME);
FORMAT DT_FORMATED DATETIME.;
FORMAT DATE DATE9.; /* NADAWANIE FORMATU DATY, FIZYCZNIE KOLUMNA NADAL JEST LICZB¥ */
FORMAT TIME TIME.;
FORMAT SAS_DT DATETIME.;
RUN;

/*RÓ¯NICA W DATACH */
DATA DATES1C;
A='05JAN2015'd;
B=A+12;
C=TODAY();
D=C-50;
IF D>C THEN E=1; ELSE E=0;
DIFF_DAY=INTCK('DAY',A,B);
DIFF_MONTH=INTCK('MONTH',A,B);
DIFF_YEAR=INTCK('YEAR',A,B);
FORMAT A DATE9.;
FORMAT B DATE9.;
FORMAT C DATE9.;
FORMAT D DATE9.;
RUN;



/* ROZPOZNAWANIE DATY Z TEKSTU */
DATA DATES2A;
   INPUT  NAME $ 1-4 @6 BDAY $11. ;
DATALINES;
John 1 1 1960
Mary 07/11/1955
Joan 07-11-1955
Kate 11.12.1962
Mark 06081959
;
RUN;

DATA DATES2B;
SET DATES2A;
DATE=INPUT(BDAY, MMDDYY11.); /* DATA JAKO LICZBA W FORMACIE SAS */
DATE_FORMATED=DATE;
DATE_AS_TEXT=PUT(DATE,DATE9.); /* DATA JAKO TEKST */
FORMAT DATE_FORMATED DATE9. ;
RUN;

/* ROZBIJANIE DATY NA SZCZEGÓ£Y */
DATA DATES2C;
SET DATES2B;
DATE_SAS=INPUT(DATE_AS_TEXT, DATE9.);
    M=MONTH(DATE_SAS);
    D=DAY(DATE_SAS) ;
    Y=YEAR(DATE_SAS);
    WK_D=WEEKDAY(DATE_SAS);
    Q=QTR(DATE_SAS);
	DROP DATE_SAS;
RUN;

/* SK£ADANIE DATY Z KILKU KOLUMN */
DATA DATES3A;
   INPUT  MONTH 1-2 YEAR 4-7 DAY 9-10;
   BDAY=MDY(MONTH,DAY,YEAR); /* DATA JAKO LICZBA W FORMACIE SAS */
DATALINES;
 7 1948 11
 1 1960  1
10 1970 15
12 1971 10
;
RUN;

DATA DATES3B;
SET DATES3A;
DATE_FORMATED=BDAY;
DATE_AS_TEXT=PUT(BDAY,DATE9.); /* DATA JAKO TEKST */
FORMAT DATE_FORMATED DATE9. ; /* NADAWANIE FORMATU DATY, FIZYCZNIE KOLUMNA NADAL JEST LICZB¥ */
RUN;


DATA DATES4A;
     INPUT COUNTRY $ 1-11 @13 DEPARTUREDATE $9. NIGHTS;
	 DATALINES;
Japan       13may2000  8
Greece      17oct99   12
New Zealand 03feb2001 16
Brazil      28feb2001  8
Venezuela   10nov00    9
Italy       25apr2001  8
USSR        03jun1997 14
Switzerland 14jan2001  9
Australia   24oct98   12
Ireland     27aug2000  7
;
RUN;


DATA DATES4B;
SET DATES4A;
DATE=INPUT(DEPARTUREDATE, DATE9.); /* DATA JAKO LICZBA W FORMACIE SAS */
DATE_FORMATED=DATE;
FORMAT DATE_FORMATED DATE7. ; /* NADAWANIE FORMATU DATY, FIZYCZNIE KOLUMNA NADAL JEST LICZB¥ */
RUN;


OPTIONS YEARCUTOFF=1920;

DATA DATES5A;
INPUT @1 RAWDATA $8. @1 DATE YYMMDD8.;
CARDS;
651128
19651128
18230314
19131225
131225
RUN;

DATA DATES5B;
SET DATES5A;
DATE_AS_TEXT=PUT(DATE, DATE9.); /* DATA JAKO TEKST */
RUN;
